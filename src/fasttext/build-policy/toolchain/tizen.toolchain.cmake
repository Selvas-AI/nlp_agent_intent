cmake_minimum_required( VERSION 2.6.3 )

macro( __INIT_VARIABLE var_name )
 set( __test_path 0 )
 foreach( __var ${ARGN} )
  if( __var STREQUAL "PATH" )
   set( __test_path 1 )
   break()
  endif()
 endforeach()
 if( __test_path AND NOT EXISTS "${${var_name}}" )
  unset( ${var_name} CACHE )
 endif()
 if( "${${var_name}}" STREQUAL "" )
  set( __values 0 )
  foreach( __var ${ARGN} )
   if( __var STREQUAL "VALUES" )
    set( __values 1 )
   elseif( NOT __var STREQUAL "PATH" )
    set( __obsolete 0 )
    if( __var MATCHES "^OBSOLETE_.*$" )
     string( REPLACE "OBSOLETE_" "" __var "${__var}" )
     set( __obsolete 1 )
    endif()
    if( __var MATCHES "^ENV_.*$" )
     string( REPLACE "ENV_" "" __var "${__var}" )
     set( __value "$ENV{${__var}}" )
    elseif( DEFINED ${__var} )
     set( __value "${${__var}}" )
    else()
     if( __values )
      set( __value "${__var}" )
     else()
      set( __value "" )
     endif()
    endif()
    if( NOT "${__value}" STREQUAL "" )
     if( __test_path )
      if( EXISTS "${__value}" )
       file( TO_CMAKE_PATH "${__value}" ${var_name} )
       if( __obsolete AND NOT _CMAKE_IN_TRY_COMPILE )
        message( WARNING "Using value of obsolete variable ${__var} as initial value for ${var_name}. Please note, that ${__var} can be completely removed in future versions of the toolchain." )
       endif()
       break()
      endif()
     else()
      set( ${var_name} "${__value}" )
       if( __obsolete AND NOT _CMAKE_IN_TRY_COMPILE )
        message( WARNING "Using value of obsolete variable ${__var} as initial value for ${var_name}. Please note, that ${__var} can be completely removed in future versions of the toolchain." )
       endif()
      break()
     endif()
    endif()
   endif()
  endforeach()
  unset( __value )
  unset( __values )
  unset( __obsolete )
 elseif( __test_path )
  file( TO_CMAKE_PATH "${${var_name}}" ${var_name} )
 endif()
 unset( __test_path )
endmacro()

__INIT_VARIABLE(TIZEN_SDK PATH ENV_TIZEN_SDK)

SET(CMAKE_SYSTEM_NAME Linux)
SET(CMAKE_SYSTEM_VERSION 1)

# device target (arm)
SET(CMAKE_C_COMPILER "${TIZEN_SDK}/tools/arm-linux-gnueabi-gcc-4.6/bin/arm-linux-gnueabi-gcc")
SET(CMAKE_CXX_COMPILER "${TIZEN_SDK}/tools/arm-linux-gnueabi-gcc-4.6/bin/arm-linux-gnueabi-g++")
SET(CMAKE_SYSROOT "${TIZEN_SDK}/platforms/tizen-2.3.1/wearable/rootstraps/wearable-2.3.1-device.core")

# emulator target (i386_x86)
#SET(CMAKE_C_COMPILER "${TIZEN_SDK}/tools/i386-linux-gnueabi-gcc-4.6/bin/i386-linux-gnueabi-gcc")
#SET(CMAKE_CXX_COMPILER "${TIZEN_SDK}/tools/i386-linux-gnueabi-gcc-4.6/bin/i386-linux-gnueabi-g++")
#SET(CMAKE_SYSROOT "${TIZEN_SDK}/platforms/tizen-2.3.1/wearable/rootstraps/wearable-2.3.1-emulator.core")

include_directories( SYSTEM "${CMAKE_SYSROOT}/usr/include" )
link_directories("${CMAKE_SYSROOT}/lib")

SET(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
SET(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
SET(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)